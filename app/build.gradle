plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' // Updated syntax
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.devtools.ksp'      // Added KSP for Room (faster/modern)
    id 'com.google.dagger.hilt.android'

    id 'com.google.gms.google-services'
}

android {
    namespace 'com.mintanable.notethepad'
    compileSdk 35

    defaultConfig {
        applicationId "com.mintanable.notethepad"
        minSdk 24
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        def localProperties = new Properties()
        def localPropertiesFile = rootProject.file("local.properties")
        if (localPropertiesFile.exists()) {
            localProperties.load(localPropertiesFile.newInputStream())
        }

        buildConfigField("String", "DRIVE_CLIENT_ID", "\"${localProperties.getProperty("DRIVE_CLIENT_ID") ?: ""}\"")
        buildConfigField("String", "DRIVE_CLIENT_SECRET", "\"${localProperties.getProperty("DRIVE_CLIENT_SECRET") ?: ""}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Updated to Java 17, required for modern Android/Gradle versions
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += "META-INF/DEPENDENCIES"
            excludes += "META-INF/LICENSE"
        }
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas")
        arg("dagger.fastInit", "enabled")
        arg("dagger.hilt.android.internal.disableAndroidSuperclassValidation", "true")
        arg("dagger.hilt.disableModulesHaveInstallInCheck", "true")
    }
}

dependencies {
    implementation 'androidx.compose.material3:material3-android:1.4.0'
    implementation 'androidx.work:work-runtime-ktx:2.10.0'

    // Use BOM to manage Compose versions automatically
    def composeBom = platform('androidx.compose:compose-bom:2024.02.01')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    // Compose dependencies from BOM
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation("androidx.compose.animation:animation:1.10.3")
    // Tooling
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Lifecycle & Navigation
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0"
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"
    implementation 'androidx.hilt:hilt-common:1.3.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // Hilt core
    implementation "com.google.dagger:hilt-android:2.57"
    ksp "com.google.dagger:hilt-android-compiler:2.57"

    // Hilt WorkManager
    implementation "androidx.hilt:hilt-work:1.3.0"
    ksp "androidx.hilt:hilt-compiler:1.3.0"

    // Room (Switched to KSP)
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    //Datastore
    implementation("androidx.datastore:datastore-preferences:1.2.0")
    // Tink for modern, hardware-backed encryption
    implementation("com.google.crypto.tink:tink-android:1.15.0")

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:34.9.0')
    implementation("com.firebaseui:firebase-ui-auth:9.0.0")
    implementation("com.google.firebase:firebase-auth")
    implementation("com.facebook.android:facebook-android-sdk:18.1.3")
    implementation("com.google.android.gms:play-services-auth:21.5.0")

    //Credential Manager
    implementation("androidx.credentials:credentials:1.5.0")
    implementation("androidx.credentials:credentials-play-services-auth:1.5.0")
    implementation("com.google.android.libraries.identity.googleid:googleid:1.2.0")

    //Coil
    implementation("io.coil-kt.coil3:coil-compose:3.3.0")
    implementation("io.coil-kt.coil3:coil-network-okhttp:3.3.0")

    //Google drive
    implementation("com.google.api-client:google-api-client-android:2.0.0")
    implementation("com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0")
    implementation("com.google.http-client:google-http-client-gson:1.42.3")
}